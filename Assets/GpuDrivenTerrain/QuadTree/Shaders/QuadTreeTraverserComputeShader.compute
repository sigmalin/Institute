#include "QuadTreeCore.hlsl"
#include "NodeDescriptor.hlsl"

// 2020.1.0a9 later
#pragma multi_compile_local __ QUAD_TREE_TRAVERSE_DISABLE

#pragma kernel TraverseQuadTree

uniform uint ConsumeNodeCount;
uniform uint PassLOD;
ConsumeStructuredBuffer<uint2> ConsumeNodeList;
AppendStructuredBuffer<uint2> AppendNodeList;
AppendStructuredBuffer<uint3> AppendFinalNodeList;

uniform float3 CameraPos;

bool EvaluateNode(uint2 node, uint lod)
{
#if defined(QUAD_TREE_TRAVERSE_DISABLE)
	return true;
#else
	float3 centerPos = CalcNodeCenterPos(node, lod);
	float size = GetLodSize(lod);
	return (length(CameraPos - centerPos) / (size * 1.414f)) < 1.0f;
#endif
}

[numthreads(64,1,1)]
void TraverseQuadTree (uint id : SV_DispatchThreadID)
{
	if (ConsumeNodeCount <= id) return;

	uint2 node = ConsumeNodeList.Consume();
	uint2 child = node << 1;
	if (PassLOD > 0 && EvaluateNode(node, PassLOD))
	{
		SetNodeBranch(node, PassLOD, MaxLOD, 1);

		AppendNodeList.Append(child);
		AppendNodeList.Append(child + uint2(1, 0));
		AppendNodeList.Append(child + uint2(0, 1));
		AppendNodeList.Append(child + uint2(1, 1));
	}
	else
	{
		SetNodeBranch(node, PassLOD, MaxLOD, 0);

		AppendFinalNodeList.Append(uint3(node, PassLOD));
	}
}
